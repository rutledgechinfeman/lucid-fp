# #################################################################################
# STEP 1: Basic skyscraper
# 10 x 50 x 10 box (scale matrix)
# tiled texture map
# box is generated by a "component split" of a facade
# this box is then repeated on Y
# tiles are repeated on X
# Pros: Really basic (5 lines of legible code)
#	Looks sort of like a highrise
# Cons: The texure map looks ridiculous
#	One size
# #################################################################################

A : NULL ~ S(10, 50, 10) _ I(4){facades}: 1.0

facades : NULL ~ Comp(sidefaces){facade} : 1.0
facade : NULL ~ Repeat(Y, 3){floor} : 1.0
floor : NULL ~ Repeat(X, 2){tile} : 1.0
tile : NULL ~ highrise : 1.0
































###################################################################################
# STEP 2: Introducing randomness
# Randomly generated size of building (for generating cities, perhaps)
# Randomly generate on and off tiles of the texture map
# Pros: Random window looks much better (stretched images)
#	Random sizes makes it more interesting
# 	Still simple (still 5 lines)
# Cons: No roof
# #################################################################################

A : NULL ~ S([10,20], [50, 70], [10,20]) _ I(4){facades}: 1.0

facades : NULL ~ Comp(sidefaces){facade} : 1.0
facade : NULL ~ Repeat(Y, 3){floor} : 1.0
floor : NULL ~ Repeat(X, 2){tile} : 1.0
tile : NULL ~ on : .1 ~ off : .9
































# #################################################################################
# STEP 3: Roofs
# New terminal and nonterminal
# Translation call (0, Scope.Y, 0)
# Pros: Roof
# Cons: No door
###################################################################################

A : NULL ~ S([10,20], [50, 70], [10,20]) _ I(4){facades} _ T(0, Scope.Y, 0) _ I(Roof.flat){roof}: 1.0

facades : NULL ~ Comp(sidefaces){facade} : 1.0
facade : NULL ~ Repeat(Y, 3){floor} : 1.0
floor : NULL ~ Repeat(X, 2){tile} : 1.0
tile : NULL ~ on : .1 ~ off : .9
roof : NULL ~ Comp(topfaces){rooftiles} : 1.0

####################################################################################





























# #################################################################################
# STEP 4: Entrance
# Subdiv command
# Condition 
# Ways to improve: more window tiles
#		   more buildings of varying height
#		   highrise awning 
#		   non-texture meshes
# ##################################################################################

A : NULL ~ S([10,20], [50, 70], [10,20]) _ I(4){facades} _ T(0, Scope.Y, 0) _ I(Roof.flat){roof}: 1.0

facades : NULL ~ Comp(sidefaces){facade} : 1.0
facade : NULL ~ Subdiv(Y, 4, 1r){ groundfloor | Repeat(Y, 3){floor} }: 1.0
groundfloor : Shape.visible(street)=full ~ entrance: 1.0
groundfloor : NULL ~ Repeat(X, 2){off} : 1.0
entrance : NULL ~ doorpic : 1.0
floor : NULL ~ Repeat(X, 2){tile} : 1.0
tile : NULL ~ on : .1 ~ off : .9
roof : NULL ~ Comp(topfaces){rooftiles} : 1.0

# ###################################################################################



























# #################################################################################
# STEP 5: Highrise!
# Stack pushing and popping
###################################################################################


A : NULL ~ S([20,59,10], 6, 16) _ I(4){facades2} _ T(0, Scope.Y, 0) _ I(Roof.flat){ roof } _ T(0, 0, Scope.Z) _ scrapers : 1.0
scrapers : NULL ~ Repeat(X, 10){ scraper } : 1.0
scraper : NULL ~ S(Scope.X, [40,70], [4,8]) _ T(0, 0, -Scope.Z) _ I(4){ facades } _ < T(0, Scope.Y, 0) _ I(Roof.flat){roof} > _ bulk : 1.0
bulk : NULL ~ S(Scope.X, [40,70], [4,8]) _ T(0, 0, -Scope.Z) _ I(4){ facades} _ T(0, Scope.Y, 0) _ I(Roof.flat){ roof } : 0.5 ~ T(0, 0, 0) : 0.5

facades : NULL ~ Comp(sidefaces){facade} : 1.0
facade : NULL ~ Repeat(Y, 3){floor} : 1.0
facades2 : NULL ~ Comp(sidefaces){groundfloor} : 1.0
groundfloor : Shape.visible(street)=full ~ T(0, 0, 1.5) _ entrances : 1.0
entrances : NULL ~ Repeat(X, 10){ entrance } : 1.0
entrance : NULL ~ Subdiv(X, 2, 1r, 1r, 1r, 2){ flank | dtile | dtile | dtile | flank } : 1.0
dtile : NULL ~ Subdiv(Y, 3, 1r) { door | flank } : 1.0
flank : NULL ~ T(0, 0, -1.5) _ on5 : 1.0
groundfloor : NULL ~ Repeat(X, 2){on5} : 1.0
floor : NULL ~ Repeat(X, 2){tile} : 1.0
floor2 : NULL ~ Repeat(X, 1){tile2} : 1.0
tile : NULL ~ on1 : 0.1 ~ on2 : 0.1 ~ on3 : 0.1 ~ on4 : .1 ~ on5 : .1  ~ on6 : .1  ~ on7 : .1 ~ on8 : .1  ~ on9 : .1 ~ on10 : .1
tile2 : NULL ~ lobby : 1.0
roof : NULL ~ Comp(topfaces){rooftiles} : 1.0























